plugins {
    id 'java'
}

group 'com.rosspaffett'
version '1.0.0+3.1.1-SNAPSHOT'

repositories {
    mavenCentral()

    maven {
        name 'velocity'
        url 'https://nexus.velocitypowered.com/repository/maven-public/'
    }
}

configurations {
    packaged
}

dependencies {
    annotationProcessor 'com.velocitypowered:velocity-api:3.0.1'

    // Dependencies that will be packaged in the JAR
    packaged 'com.google.code.gson:gson:2.8.9'
    packaged 'org.snakeyaml:snakeyaml-engine:2.3'

    configurations.implementation.extendsFrom(configurations.packaged)
    implementation 'com.velocitypowered:velocity-api:3.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes([
                'Implementation-Title': rootProject.name,
                'Implementation-Version': rootProject.version
        ])
    }

    from {
        configurations.packaged.filter {
            it.exists()
        }.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
